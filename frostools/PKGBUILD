# Maintainer: Chaitanya Rahalkar <chaitanya@rahalkar.xyz>

pkgbase=frostools
pkgname=('frostools-base' 'frostools-pkg' 'frostools-iso')
pkgver=0.20.4
pkgrel=1
arch=('any')
pkgdesc='Development tools for Frost Linux'
license=('GPL')
groups=('frostools')
url='https://github.com/frost-linux/frostools'
source=("${pkgbase}-${pkgver}.zip::${url}/archive/${pkgver}.zip")
sha256sums=('31FF2687A51CDEADF4C0ED92628C9B70F54A9AF358B15903C3DB99E9677B628E')

prepare() {
    cd ${pkgbase}-${pkgver}
    sed -e "s/^VERSION=.*/VERSION=${pkgver}/" -i Makefile
}

build() {
    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr
}

package_frostools-base() {
    pkgdesc='Development tools for Frost Linux (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('frostools-pkg: Frost Linux package tools'
                'frostools-iso: Frost Linux iso tools')
    backup=('etc/frostools/frostools-base.conf')
    conflicts=('arch-install-scripts')
    # install=frostools.install

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_frostools-pkg() {
    pkgdesc='Development tools for Frost Linux (packaging tools)'
    depends=('frostools-base' 'namcap' 'git-subrepo' 'jq')
    conflicts=('devtools')
    backup=('etc/frostools/frostools-pkg.conf')

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_frostools-iso() {
    pkgdesc='Development tools for Frost Linux (ISO tools)'
    depends=('frostools-base' 'iso-profiles' 'grub' 'mkinitcpio'
            'dosfstools' 'squashfs-tools' 'libisoburn')
    conflicts=('archiso')
    backup=('etc/frostools/frostools-iso.conf')

    cd ${pkgbase}-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
