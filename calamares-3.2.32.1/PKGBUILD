# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: nous <nous@artixlinux.org>

pkgname=calamares
pkgver=3.2.32.1
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
license=(GPL)
url="https://github.com/calamares/calamares"
license=('LGPL')
depends=('yaml-cpp' 'kpmcore>=3.1.0' 'boost-libs' 'hwinfo' 'gtk-update-icon-cache'
          'libpwquality' 'polkit-qt5' 'qt5-xmlpatterns' 'plasma-framework')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
optdepends=('calamares-branding: Artix branding')
source=("${url}/archive/v$pkgver.tar.gz"
        0001-artix-modules.patch)
sha256sums=('898ff375bc679ad4be8214466931662a171809fa147d8fdd1ecc9841a1f9dbe4'
            '0e0b376e3f32c60eb962e79076d93786f0bd5ea0e8bdea66e69a633e9fe89a08')

prepare() {
    cd $pkgname-$pkgver
    # patches here
    patch -Np 1 -i $srcdir/0001-artix-modules.patch
}

build() {
	cd $pkgname-$pkgver

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=OFF \
              -DINSTALL_CONFIG:BOOL=ON \
              -DINSTALL_POLKIT:BOOL=ON \
              -DBoost_NO_BOOST_CMAKE=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg packagechooser services-systemd \
                              dummyprocess dummypython dummycpp dummypythonqt"
        make
}

package() {
	cd $pkgname-$pkgver/build
	make DESTDIR="$pkgdir" install

    cd ${srcdir}/$pkgname-$pkgver
}
